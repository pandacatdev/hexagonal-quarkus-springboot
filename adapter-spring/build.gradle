plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.hexagonal.shop'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


configurations {
    tests {
        canBeConsumed = true
        canBeResolved = false
    }
}

tasks.register('testJar', Jar) {
    archiveClassifier.set('tests')
    from sourceSets.test.output
    dependsOn tasks.testClasses
}

artifacts {
    tests tasks.named('testJar')
}

dependencies {
    // Version management (Quarkus BOM)
    implementation enforcedPlatform("io.quarkus.platform:quarkus-bom:${quarkusPlatformVersion}")

    // Internal hexagonal core modules
    implementation project(':model')
    implementation project(':application')

    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'

    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // --- Test dependencies ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.testcontainers:mysql:1.21.3'

    // Use the "attached test JAR" from the "model" module
    testImplementation project(path: ':model', configuration: 'tests')

    // Lombok for tests
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

test {
    useJUnitPlatform()
}