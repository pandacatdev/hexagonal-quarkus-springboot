plugins {
    id 'java-library'
    id 'io.quarkus' version "${quarkusPlatformVersion}"
}

group = 'com.hexagonal.shop'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}


configurations {
    tests {
        canBeConsumed = true
        canBeResolved = false
    }
}

tasks.register('testJar', Jar) {
    archiveClassifier.set('tests')
    from sourceSets.test.output
    dependsOn tasks.testClasses
}

artifacts {
    tests tasks.named('testJar')
}

dependencies {
    // Version management (Quarkus BOM)
    implementation enforcedPlatform("io.quarkus.platform:quarkus-bom:${quarkusPlatformVersion}")

    // Internal hexagonal core modules
    implementation project(':model')
    implementation project(':application')

    // Quarkus runtime
    api "io.quarkus:quarkus-arc"
    api "io.quarkus:quarkus-resteasy"
    api "io.quarkus:quarkus-resteasy-jackson"
    api "io.quarkus:quarkus-hibernate-orm"
    api "io.quarkus:quarkus-hibernate-orm-panache"
    api "io.quarkus:quarkus-jdbc-mysql"

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // Test stack
    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.quarkus:quarkus-junit5-mockito"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation 'org.assertj:assertj-core:3.27.6'

    // Testcontainers for MySQL integration tests
    testImplementation 'org.testcontainers:mysql:1.21.3'

    // model's test-jar
    testImplementation project(path: ':model', configuration: 'tests')

    // Lombok for tests
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

test {
    useJUnitPlatform()
}